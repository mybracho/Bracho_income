
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТабличныйДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	МассивДанных = ПолучитьДанныеНаСервере();
	Закрыть(МассивДанных);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеНаСервере()

	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Наименование");
	ТаблицаЗначений.Колонки.Добавить("Часов",Новый ОписаниеТипов("Число")); 
	ТаблицаЗначений.Колонки.Добавить("Цена",Новый ОписаниеТипов("Число"));
	ТаблицаЗначений.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число"));


	ВысотаТаблицы = ДанныеИмпорта.ВысотаТаблицы;
	Для Сч = 2 По ВысотаТаблицы Цикл
		Наименование	= ДанныеИмпорта.Область("R" + Сч + "C1").Текст;
		Часов			= ДанныеИмпорта.Область("R" + Сч + "C3").Текст;
		Цена			= ДанныеИмпорта.Область("R" + Сч + "C4").Текст;
		Сумма			= ДанныеИмпорта.Область("R" + Сч + "C5").Текст;

		НоваяСтрока = ТаблицаЗначений.Добавить();
		НоваяСтрока.Наименование	= Наименование;
		НоваяСтрока.Часов			= СтрокаВЧисло(Часов);
		НоваяСтрока.Цена			= СтрокаВЧисло(Цена);
		НоваяСтрока.Сумма			= СтрокаВЧисло(Сумма);

	КонецЦикла;
	МассивСтруктур = ПреобразоватьТаблицуЗначенийВМассивСтруктур(ТаблицаЗначений);
	Возврат МассивСтруктур;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаВЧисло(ЧислоКакСтрока) 
	Если СокрЛП(ЧислоКакСтрока) = "" Тогда
		Возврат 0;
	КонецЕсли;	
	ЧислоКакЧисло = СтрЗаменить(ЧислоКакСтрока,Символы.НПП,"");
	ЧислоКакЧисло = СтрЗаменить(ЧислоКакЧисло," ","");

	Возврат Число(ЧислоКакЧисло);
КонецФункции 

&НаСервере
Функция ПреобразоватьТаблицуЗначенийВМассивСтруктур(ТаблицаЗначений)

	Массив = Новый Массив();

	СтруктураСтрокой = "";
	НужнаЗапятая 	 = Ложь;

	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл

		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;

		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая = Истина;

	КонецЦикла;

	Для Каждого Строка Из ТаблицаЗначений Цикл

		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);

		Массив.Добавить(НоваяСтрока);

	КонецЦикла;

	Возврат Массив;

КонецФункции

&НаСервере
Процедура ЗаполнитьТабличныйДокумент()
	
	Макет = Документы.Счет.ПолучитьМакет("ШаблонИмпорт");
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ДанныеИмпорта.Вывести(ОблШапка);
	
КонецПроцедуры

#КонецОбласти	




