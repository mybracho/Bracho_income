#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СоздатьУсловноеОформление();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьУсловноеОформление()
	СоответствиеЦветов = Новый Соответствие;
	СоответствиеЦветов.Вставить(Перечисления.СтатусыСчета.ВРаботе, ЦветаПалитры.Желтый);
	СоответствиеЦветов.Вставить(Перечисления.СтатусыСчета.ОжиданиеОплаты, ЦветаПалитры.Оранжевый);
	СоответствиеЦветов.Вставить(Перечисления.СтатусыСчета.ВыставитьСчет, ЦветаПалитры.Зеленый);
	СоответствиеЦветов.Вставить(Перечисления.СтатусыСчета.Оплачен, ЦветаПалитры.Голубой);
	
	Для Каждого ЭлементСоответствия Из СоответствиеЦветов Цикл
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		
		Отбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СтатусДокумента");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = ЭлементСоответствия.Ключ;
		Отбор.Использование = Истина;
		
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЭлементСоответствия.Значение);
		
		ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("СтатусДокумента");
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти

