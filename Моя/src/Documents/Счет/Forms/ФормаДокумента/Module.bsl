//@skip-check reading-attribute-from-database
//@skip-check use-non-recommended-method

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		Объект.КурсВалюты = 1;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КурсВалютыПриИзменении(Элемент)
	ПересчитатьСуммуДокументаОтВалюты();
КонецПроцедуры

&НаКлиенте
Процедура ЧасоваяСтавкаПриИзменении(Элемент)
	ПересчитатьСуммуДокументаОтВалюты();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	УстановитьСтавкуЗаЧас();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНоменклатура

&НаКлиенте
Процедура НоменклатураЧасовПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Номенклатура.ТекущиеДанные;
	ПересчетатьСумму(ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Номенклатура.ТекущиеДанные;
	ПересчетатьСумму(ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		СтрокаНоменклатуры = Элементы.Номенклатура.ТекущиеДанные;
		СтрокаНоменклатуры.Цена = Объект.ЧасоваяСтавка;
		СтрокаНоменклатуры.ДатаРаботы = ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Импорт(Команда)
    ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьТекстЗавершение", ЭтотОбъект);    
    ОткрытьФорму("Документ.Счет.Форма.ФормаИмпортаДанных", , , , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСчета(Команда)
	
	ТабДок = ПечатьСчетаНаСервере();
	ТабДок.Показать("Счет на оплату");
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьАкта(Команда)
	
	ТабДок = ПечатьАктаНаСервере();
	ТабДок.Показать("Акт выполненных работ");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПакет(Команда)
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытия.Заголовок = "Выберите каталог";
	ДиалогОткрытия.МножественныйВыбор = Ложь;
	
	СтруктураПараметров = Новый Структура;
	Оповещение = Новый ОписаниеОповещения("ВыборКаталогаДляЭкспортаЗавершение", ЭтотОбъект, СтруктураПараметров);
	ДиалогОткрытия.Показать(Оповещение)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоВыполненнымРаботам(Команда)
	СтруктураПараметров = Новый Структура("Контрагент", Объект.Контрагент);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораСпискаРабот", ЭтотОбъект, СтруктураПараметров);
	
	ОткрытьФорму("Документ.Счет.Форма.ФормаПодбораВыполненныхРабот",
	СтруктураПараметров,
	ЭтотОбъект,
	,
	,
	,ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДоговора(Команда)
	ТабДок = ПолучитьТабДокДоговора();
	ТабДок.Показать("Печать договора");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНомер(Команда)
	УстановитьНомерНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ДобавитьТекстЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт     
    Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
        Объект.Номенклатура.Очистить();
        
        Для Каждого ЭлементМассива Из РезультатЗакрытия Цикл
            ЗаполнитьЗначенияСвойств(ОБъект.Номенклатура.Добавить(),ЭлементМассива);
        КонецЦикла; 
    КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураКонтрагента(КонтрагентСсылка)
	
	Структура = Новый Структура;
	Структура.Вставить("КонтрагентСсылка");
	Структура.Вставить("Наименование");
	Структура.Вставить("НаименованиеДляПечати");
	Структура.Вставить("ИНН");
	Структура.Вставить("КПП");
	Структура.Вставить("Индекс");
	Структура.Вставить("Адрес");
	Структура.Вставить("Телефон");
	Структура.Вставить("ДолжностьРуководителя");
	Структура.Вставить("Руководитель");

	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонтрагентСсылка", КонтрагентСсылка);
	Запрос.Текст = 
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК КонтрагентСсылка,
			|	Контрагенты.Наименование КАК Наименование,
			|	Контрагенты.ПолноеНаименование КАК НаименованиеДляПечати,
			|	Контрагенты.ИНН КАК ИНН,
			|	Контрагенты.КПП КАК КПП,
			|	Контрагенты.Индекс КАК Индекс,
			|	Контрагенты.Адрес КАК Адрес,
			|	Контрагенты.Телефон КАК Телефон,
			|	Контрагенты.КонтрагентДолжностьРуководителя КАК ДолжностьРуководителя,
			|	Контрагенты.КонтрагентРуководитель КАК Руководитель
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Ссылка = &КонтрагентСсылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(Структура, Выборка);
		
	КонецЕсли;
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПечатнаяИнформацияКонтрагента(СтруктураКонтрагента)
	
	ПечатнаяИнформация = "";
	ПечатнаяИнформация = ПечатнаяИнформация+СтруктураКонтрагента.НаименованиеДляПечати+", ";
	ПечатнаяИнформация = ПечатнаяИнформация+" ИНН "+СтруктураКонтрагента.ИНН+", ";
	Если ЗначениеЗаполнено(СтруктураКонтрагента.КПП) Тогда
		ПечатнаяИнформация = ПечатнаяИнформация+" КПП "+СтруктураКонтрагента.КПП+", ";
	КонецЕсли;
	ПечатнаяИнформация = ПечатнаяИнформация+СтруктураКонтрагента.Индекс+", ";
	ПечатнаяИнформация = ПечатнаяИнформация+СтруктураКонтрагента.Адрес;
	Если ЗначениеЗаполнено(СтруктураКонтрагента.Телефон) Тогда
		ПечатнаяИнформация = ПечатнаяИнформация+", ";
		ПечатнаяИнформация = ПечатнаяИнформация+" тел.: "+СтруктураКонтрагента.Телефон;
	КонецЕсли;
	Возврат ПечатнаяИнформация;
	
КонецФункции

&НаСервере
Функция ПредставлениеОрганизации(ОрганизацияСсылка)
	ПредставлениеОрганизации = СтрШаблон("%1, ИНН %2, %3",
	ОрганизацияСсылка.Наименование,
	ОрганизацияСсылка.ИНН,
	ОрганизацияСсылка.ЮридическийАдрес);
	
	Возврат ПредставлениеОрганизации;
КонецФункции

&НаСервере
Функция ПечатьСчетаНаСервере()
	
	ТабДок		= Новый ТабличныйДокумент;
	Макет		= Документы.Счет.ПолучитьМакет("Счет");
	ОблШапка 	= Макет.ПолучитьОбласть("Шапка");
	ОблСтрока	= Макет.ПолучитьОбласть("Строка");
	ОблПодвал	= Макет.ПолучитьОбласть("Подвал");
	
	СтруктураКонтрагента = СтруктураКонтрагента(Объект.Контрагент);

	ОблШапка.Параметры.НомерСчета				= СокрЛП(Объект.Номер);
	ОблШапка.Параметры.ДатаСчета				= Формат(Объект.Дата,"ДФ=dd.MM.yyyy");
	ОблШапка.Параметры.ПредставлениеКонтрагента = ПечатнаяИнформацияКонтрагента(СтруктураКонтрагента);
	ОблШапка.Параметры.Договор					= Объект.Договор.Наименование;
	ОблШапка.Параметры.ОрганизацияПолучатель	= Объект.Организация.Наименование;
	ОблШапка.Параметры.БИКПолучатель 			= Объект.БанковскийСчет.БИК;
	ОблШапка.Параметры.КорСчетПолучатель		= Объект.БанковскийСчет.НомерКорСчета;
	ОблШапка.Параметры.СчетПолучатель			= Объект.БанковскийСчет.НомерСчета;
	ОблШапка.Параметры.ИННОрганизации			= Объект.Организация.ИНН;
	ОблШапка.Параметры.ИнформацияПоставщик		= ПредставлениеОрганизации(Объект.Организация);
	ТабДок.Вывести(ОблШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВТ_Номенклатура", Объект.Номенклатура.Выгрузить());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ_Номенклатура.Наименование КАК Наименование,
	|	ВТ_Номенклатура.Часов КАК Часов,
	|	ВТ_Номенклатура.Цена КАК Цена,
	|	ВТ_Номенклатура.Сумма КАК Сумма,
	|	ВТ_Номенклатура.ДатаРаботы КАК ДатаРаботы
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	&ВТ_Номенклатура КАК ВТ_Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Наименование КАК СТРОКА(200)) КАК Наименование,
	|	СУММА(ВТ_Номенклатура.Часов) КАК Часов,
	|	ВТ_Номенклатура.Цена КАК Цена,
	|	СУММА(ВТ_Номенклатура.Сумма) КАК Сумма,
	|	ВТ_Номенклатура.ДатаРаботы КАК ДатаРаботы
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Номенклатура.Цена,
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Наименование КАК СТРОКА(200)),
	|	ВТ_Номенклатура.ДатаРаботы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРаботы";

	НомерПП = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НомерПП = НомерПП+1;
		ОблСтрока.Параметры.НомерПП = НомерПП;
		ЗаполнитьЗначенияСвойств(ОблСтрока.Параметры, Выборка);
		ОблСтрока.Параметры.Наименование = ПолучитьПредставлениеНоменклатуры(Выборка.Наименование, Выборка.ДатаРаботы);
		
		ТабДок.Вывести(ОблСтрока);
	КонецЦикла;

	СуммаИтог = Объект.Номенклатура.Итог("Сумма");

	ОблПодвал.Параметры.КоличествоУслуг	= Объект.Номенклатура.Количество();
	ОблПодвал.Параметры.ИтогСумма		= СуммаИтог;
	ОблПодвал.Параметры.СуммаПрописью	= ЧислоПрописью(СуммаИтог,"Л=ru_RU")+" рублей";
	ОблПодвал.Параметры.ФИОРуководителя	= Объект.Организация.Руководитель;
	
	ТабДок.Вывести(ОблПодвал);
	ТабДок.ОтображатьЗаголовки	= Ложь;
	ТабДок.ОтображатьСетку		= Ложь;
	ТабДок.АвтоМасштаб			= Истина;
	
	Если Объект.Контрагент.флагНеВыводитьКолонкуЦенВАктах Тогда
		
		Для Сч = 27 По 29 Цикл
			ТабДок.Область("C"+Сч).ШиринаКолонки = 0;
			ТабДок.Область("C"+Сч).Видимость = Ложь;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Объект.флагВыводитьПодпись Тогда
		ТабДок.Рисунки.Подпись.Картинка			= Новый Картинка(Объект.Организация.Подпись.ДанныеКартинки.Получить());
		ТабДок.Рисунки.Подпись.Линия			= Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
	КонецЕсли;
	
	Возврат ТабДок;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеНоменклатуры(НоменклатураНаименование, ДатаРаботы)
	Представление = НоменклатураНаименование;
	Если ЗначениеЗаполнено(ДатаРаботы) Тогда
		Представление = СтрШаблон("%1 (%2)", Представление, Формат(ДатаРаботы, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	Возврат Представление;
КонецФункции

&НаСервере
Функция ИнформацияОДоговоре()
	Возврат СтрШаблон("Договор №%1 от %2", СокрЛП(Объект.Договор.НомерДоговора), Формат(Объект.Договор.ДатаДоговора, "ДФ=dd.MM.yyyy"));
КонецФункции

&НаСервере
Функция ПечатьАктаНаСервере()
	
	СтруктураКонтрагента = СтруктураКонтрагента(Объект.Контрагент);
	
	ТабДок		= Новый ТабличныйДокумент;
	Макет		= Документы.Счет.ПолучитьМакет("Акт");
	ОблШапка	= Макет.ПолучитьОбласть("Шапка");
	ОблСтрока	= Макет.ПолучитьОбласть("Строка");
	ОблПодвал	= Макет.ПолучитьОбласть("Подвал");

	ОблШапка.Параметры.НомерСчета = СокрЛП(Объект.Номер);
	ОблШапка.Параметры.ДатаСчета  = Формат(Объект.Дата,"ДФ=dd.MM.yyyy");
	ОблШапка.Параметры.ИнформацияОДоговоре = ИнформацияОДоговоре();

	ТабДок.Вывести(ОблШапка);

	НомерПП = 0;
	
	ТаблицаДанные = Объект.Номенклатура.Выгрузить();
	ТаблицаДанные.Сортировать("ДатаРаботы");
	
	Для Каждого СтрокаТЧ Из ТаблицаДанные Цикл
		
		НомерПП = НомерПП+1;
		ОблСтрока.Параметры.НомерПП = НомерПП;
		ЗаполнитьЗначенияСвойств(ОблСтрока.Параметры,СтрокаТЧ);
		ОблСтрока.Параметры.Наименование = ПолучитьПредставлениеНоменклатуры(СтрокаТЧ.Наименование, СтрокаТЧ.ДатаРаботы);
		ТабДок.Вывести(ОблСтрока);
		
	КонецЦикла;

	СуммаИтог = Объект.Номенклатура.Итог("Сумма");

	ОблПодвал.Параметры.КоличествоУслуг					= Объект.Номенклатура.Количество();
	ОблПодвал.Параметры.ИтогСумма						= СуммаИтог;
	ОблПодвал.Параметры.СуммаПрописью					= ЧислоПрописью(СуммаИтог,"Л=ru_RU")+" рублей";
	ОблПодвал.Параметры.КонтрагентДолжностьРуководителя	= СтруктураКонтрагента.ДолжностьРуководителя;
	ОблПодвал.Параметры.Контрагент						= СтруктураКонтрагента.НаименованиеДляПечати;
	ОблПодвал.Параметры.КонтрагентРуководитель			= СтруктураКонтрагента.Руководитель;
	ОблПодвал.Параметры.ФИОРуководителя					= Объект.Организация.Руководитель;

	ТабДок.Вывести(ОблПодвал);
	
	Если Объект.Контрагент.флагНеВыводитьКолонкуЦенВАктах Тогда
		
		Для Сч = 27 По 29 Цикл
			ТабДок.Область("C"+Сч).ШиринаКолонки = 0;
			ТабДок.Область("C"+Сч).Видимость = Ложь;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Объект.флагВыводитьПодпись Тогда
		ТабДок.Рисунки.Подпись.Картинка			= Новый Картинка(Объект.Организация.Подпись.ДанныеКартинки.Получить());;
		ТабДок.Рисунки.Подпись.Линия			= Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
	КонецЕсли;
	
	ТабДок.ОтображатьЗаголовки	= Ложь;
	ТабДок.ОтображатьСетку		= Ложь;
	ТабДок.АвтоМасштаб			= Истина;
	
	
	Возврат ТабДок;
	
КонецФункции

&НаКлиенте
Процедура ПересчетатьСумму(СтрокаТЧ)
	СтрокаТЧ.Сумма = СтрокаТЧ.Часов*СтрокаТЧ.Цена;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыборКаталогаДляЭкспортаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПутьККаталогу = Результат[0];
	
	ПрефиксФайлов = ОпределитьПрефиксФайлаПоОрганизации(Объект.Организация);
	
	НомерИДата = СтрШаблон("%1_от_%2", УдалитьЗапрещенныеСимволыИзСтроки(Объект.Номер), Формат(Объект.Дата, "ДФ=dd_MM_yyyy"));
	
	ИмяФайла = СтрШаблон("%1\%2 Счет %3.pdf", ПутьККаталогу, ПрефиксФайлов, НомерИДата);
	ТабДокСчет = ПечатьСчетаНаСервере();
	ТабДокСчет.ЗаписатьАсинх(ИмяФайла, ТипФайлаТабличногоДокумента.PDF);
	
	ИмяФайла = СтрШаблон("%1\%2 Акт %3.pdf", ПутьККаталогу, ПрефиксФайлов, НомерИДата);
	ТабДокАкт = ПечатьАктаНаСервере();
	ТабДокАкт.ЗаписатьАсинх(ИмяФайла, ТипФайлаТабличногоДокумента.PDF);
	
	ВывестиСообщение(СтрШаблон("Сохранение успешно сохранено по пути %1", ПутьККаталогу));	
КонецПроцедуры

&НаКлиенте
Функция УдалитьЗапрещенныеСимволыИзСтроки(Строка)
	МассивСимволов = Новый Массив;
	МассивСимволов.Добавить("/");
	МассивСимволов.Добавить("\");
	
	НоваяСтрока = Строка;
	Для Каждого ЗаменяемыйСимвол Из МассивСимволов Цикл
		НоваяСтрока = СтрЗаменить(НоваяСтрока, ЗаменяемыйСимвол, "");
	КонецЦикла;
	
	Возврат НоваяСтрока;
	
	
КонецФункции

&НаСервереБезКонтекста
Функция ОпределитьПрефиксФайлаПоОрганизации(ОрганизацияСсылка)
	МассивЗамен = Новый Массив;
	МассивЗамен.Добавить(" ");
	МассивЗамен.Добавить(",");
	МассивЗамен.Добавить(".");
	
	Префикс = ОрганизацияСсылка.Наименование;
	Для Каждого ЭлементМассива Из МассивЗамен Цикл
		Префикс = СтрЗаменить(Префикс, ЭлементМассива, "");
	КонецЦикла;
	Возврат Префикс;
КонецФункции

&НаСервере
Процедура ПересчитатьСуммуДокументаОтВалюты()
	Для Каждого СтрокаТЧ Из Объект.Номенклатура Цикл
		СтрокаТЧ.Сумма = Окр(СтрокаТЧ.Часов * Объект.ЧасоваяСтавка * Объект.КурсВалюты, 0);
	КонецЦикла;
	Если Не Объект.флагОплатаПоКурсу Тогда
		Возврат;
	КонецЕсли;
	Объект.СуммаДокументаВалюта = Объект.Номенклатура.Итог("Часов") * Объект.ЧасоваяСтавка;
	Объект.Комментарий = СтрШаблон("%1*%2 = %3. %4*%5 = %6",
	Объект.ЧасоваяСтавка,
	Объект.Номенклатура.Итог("Часов"),
	Объект.СуммаДокументаВалюта,
	Объект.СуммаДокументаВалюта,
	Объект.КурсВалюты,
	Объект.Номенклатура.Итог("Сумма"));
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСпискаРабот(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Результат Цикл
			НоваяСтрока = Объект.Номенклатура.Добавить();
			НоваяСтрока.Наименование	= ЭлементМассива.Задача;
			НоваяСтрока.Часов			= ЭлементМассива.Часов;
			НоваяСтрока.Цена			= Объект.ЧасоваяСтавка;
			ПересчетатьСумму(НоваяСтрока);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьСтавкуЗаЧас()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Валюта", Объект.Валюта);
	Запрос.УстановитьПараметр("ДатаЗапроса", Объект.Дата);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтавкаЗаЧасСрезПоследних.Ставка КАК Ставка
	|ИЗ
	|	РегистрСведений.СтавкаЗаЧас.СрезПоследних(
	|			&ДатаЗапроса,
	|			Контрагент = &Контрагент
	|				И Валюта = &Валюта) КАК СтавкаЗаЧасСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.ЧасоваяСтавка = Выборка.Ставка;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьТабДокДоговора()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = Документы.Счет.ПолучитьМакет("Договор");
	
	ОбластьЧасть1 = Макет.ПолучитьОбласть("Часть1");
	ОбластьЧасть2 = Макет.ПолучитьОбласть("Часть2");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ОбластьЧасть1.Параметры.НомерДоговора	= Объект.Договор.НомерДоговора;
	ОбластьЧасть1.Параметры.ДатаДоговора	= Формат(Объект.Договор.ДатаДоговора, "ДФ=dd.MM.yyyy");
	ОбластьЧасть1.Параметры.Контрагент		= Объект.Контрагент.ПолноеНаименование;
	
	ТабличныйДокумент.Вывести(ОбластьЧасть1);

	
	Счетчик = 0;
	СуммаУслуг = 0;
	
	ТаблицаНоменклатура = Объект.Номенклатура.Выгрузить();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатура", ТаблицаНоменклатура);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНоменклатура.Наименование КАК Наименование,
	|	ТаблицаНоменклатура.ДатаРаботы КАК ДатаРаботы,
	|	ТаблицаНоменклатура.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	&ТаблицаНоменклатура КАК ТаблицаНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Наименование КАК СТРОКА(500)) КАК Наименование,
	|	МИНИМУМ(ВТ_Номенклатура.ДатаРаботы) КАК ДатаРаботыМинимальная,
	|	МАКСИМУМ(ВТ_Номенклатура.ДатаРаботы) КАК ДатаРаботыМаксимальная,
	|	СУММА(ВТ_Номенклатура.Сумма) КАК Сумма
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Наименование КАК СТРОКА(500))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Счетчик = Счетчик + 1;
		
		ОбластьСтрока.Параметры.НомерПП			= Счетчик;
		ОбластьСтрока.Параметры.ОписаниеЗадачи	= Выборка.Наименование;
		ОбластьСтрока.Параметры.ДатаРабот		= СтрШаблон("%1 - %2", 
		Формат(Выборка.ДатаРаботыМинимальная, "ДФ=dd.MM.yyyy"),
		Формат(Выборка.ДатаРаботыМаксимальная, "ДФ=dd.MM.yyyy"));
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
		
		СуммаУслуг = СуммаУслуг + Выборка.Сумма;
	КонецЦикла;
	
	ОбластьЧасть2.Параметры.СуммаЦифрами		= СуммаУслуг;
	ОбластьЧасть2.Параметры.СуммаПрописью		= ОбщегоНазначения.ПрописьЧисла(СуммаУслуг, Объект.Валюта);
	ОбластьЧасть2.Параметры.ФИОДиректора		= Объект.Контрагент.КонтрагентРуководитель;
	
	ТабличныйДокумент.Вывести(ОбластьЧасть2);
	
	ТабличныйДокумент.ОтображатьЗаголовки	= Ложь;
	ТабличныйДокумент.ОтображатьСетку		= Ложь;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	
	Возврат ТабличныйДокумент;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиСообщение(ТекстСообщения)

	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = (ТекстСообщения);
	Сообщение.Сообщить();

КонецПроцедуры

&НаСервере
Процедура УстановитьНомерНаСервере()
	ЗаказОбъект = РеквизитФормыВЗначение("Объект");
	Объект.Номер = ЗаказОбъект.ПолучитьНовыйНомер();
	
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти
























































