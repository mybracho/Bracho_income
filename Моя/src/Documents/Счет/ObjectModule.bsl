#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	СтатусДокумента = Перечисления.СтатусыСчета.ВРаботе;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	СтатусДокумента = Перечисления.СтатусыСчета.ВРаботе;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураТолькоМойДоход = Номенклатура.Выгрузить(Новый Структура("НеУчитыватьВДодохе", Ложь));
	СуммаДокумента = НоменклатураТолькоМойДоход.Итог("Сумма");
	
	КурсКонверсии = ОбщегоНазначения.КроссКурс(Валюта, Справочники.Валюты.USD, Дата);
	
	СуммаЭквивалент = СуммаДокумента / КурсКонверсии;
	
	Если ЭтоНовый() Тогда
		Номер = ПолучитьНовыйНомер();
	КонецЕсли;		
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	ДвиженияПоРегиструДоходов();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДвиженияПоРегиструДоходов()
	КурсКонверсии = ОбщегоНазначения.КроссКурс(Валюта, Справочники.Валюты.USD, Дата);
	
	Таблица = Номенклатура.Выгрузить(Новый Структура("НеУчитыватьВДодохе", Ложь));
	СуммаДохода = Таблица.Итог("Сумма");
	СуммаДоходаЭквивалент = СуммаДохода / КурсКонверсии;
	
	// регистр Доходы 
	Движения.Доходы.Записывать = Истина;
	Движение					= Движения.Доходы.Добавить();
	Движение.Валюта				= Валюта;
	Движение.Период				= Дата;
	Движение.Контрагент			= Контрагент;
	Движение.СуммаДохода		= СуммаДохода;
	Движение.СуммаЭквивалент	= СуммаДоходаЭквивалент;
КонецПроцедуры

Функция ПолучитьНовыйНомер() Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НачалоПериод", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(Счет.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	Документ.Счет КАК Счет
	|ГДЕ
	|	Счет.Дата МЕЖДУ &НачалоПериод И &КонецПериода
	|	И НЕ Счет.ПометкаУдаления
	|	И Счет.Ссылка <> &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат СтрШаблон("%1/%2", Месяц(Дата), Формат(Выборка.КоличествоДокументов + 1, "ЧЦ=2; ЧВН=; ЧГ=0"));
	Иначе
		Возврат СтрШаблон("%1/%2", Месяц(Дата), "01");	
	КонецЕсли;	
КонецФункции	

#КонецОбласти

#КонецЕсли


