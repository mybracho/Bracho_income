
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТаблицыНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьТаблицы(Команда)
	ПрочитатьТаблицыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройки(Команда)
	ЗаполнитьНастройкиНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиНаСервере()
	ТаблицаПолейГруппировок.Очистить();
	ТаблицаПолейСумм.Очистить();
	
	ТаблицаЗначений1Заголовки = ПолучитьДанныеТаблицы("ТабличныйДокумент1", Истина);
	ТаблицаЗначений2Заголовки = ПолучитьДанныеТаблицы("ТабличныйДокумент2", Истина);
	
	Для Каждого Колонка Из ТаблицаЗначений1Заголовки.Колонки Цикл
		ИмяКолонки = Колонка.Имя;
		Если ТаблицаПолейГруппировок.НайтиСтроки(Новый Структура("ИмяГруппировки", ИмяКолонки)).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаПолейГруппировок.Добавить();
		НоваяСтрока.ИмяГруппировки = ИмяКолонки; 
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеТаблицы(ИмяТаблицы, флагТолькоЗаголовки = Ложь) 
	//Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область(тут можешь задать начальные и конечные границы области, указав номер строик и номер колонки));

	ТабличныйДокумент = ЭтотОбъект[ИмяТаблицы];
	Если ТабличныйДокумент.ВысотаТаблицы = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;	
	
	Построитель = Новый ПостроительЗапроса;
	Если флагТолькоЗаголовки Тогда
		Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область(1, ,1));
	Иначе
		Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область());
	КонецЕсли;	
	Построитель.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	Построитель.ЗаполнитьНастройки();
	Построитель.Выполнить();
	
	ТаблицаЗначений = Построитель.Результат.Выгрузить();
	МассивПустыхСтрок = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		флагПустаяСтрока = Истина;
		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы[Колонка.Имя]) Тогда
				флагПустаяСтрока  = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если флагПустаяСтрока Тогда
			МассивПустыхСтрок.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	Для Каждого ПустаяСтрока Из МассивПустыхСтрок Цикл
		ТаблицаЗначений.Удалить(ПустаяСтрока);	
	КонецЦикла;	
	
	Возврат ТаблицаЗначений;
КонецФункции	

&НаКлиенте
Процедура ТаблицаПолейГруппировокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаТЗ = ТаблицаПолейГруппировок.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ЗначениеКолонки = СтрокаТЗ.ИмяГруппировки;
	Если ТаблицаПолейСумм.НайтиСтроки(Новый Структура("ИмяКолонки", ЗначениеКолонки)).Количество() = 0 Тогда
		НоваяСтрока = ТаблицаПолейСумм.Добавить();
		НоваяСтрока.ИмяКолонки = ЗначениеКолонки; 
	КонецЕсли;
	ТаблицаПолейГруппировок.Удалить(СтрокаТЗ);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПолейСуммВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаТЗ = ТаблицаПолейСумм.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ЗначениеКолонки = СтрокаТЗ.ИмяКолонки;
	Если ТаблицаПолейГруппировок.НайтиСтроки(Новый Структура("ИмяГруппировки", ЗначениеКолонки)).Количество() = 0 Тогда
		НоваяСтрока = ТаблицаПолейГруппировок.Добавить();
		НоваяСтрока.ИмяГруппировки = ЗначениеКолонки; 
	КонецЕсли;
	ТаблицаПолейСумм.Удалить(СтрокаТЗ);
КонецПроцедуры

&НаСервере
Процедура СравнитьТаблицыНаСервере()
	МассивГруппировки = ТаблицаПолейГруппировок.Выгрузить().ВыгрузитьКолонку("ИмяГруппировки");
	МассивСумм = ТаблицаПолейСумм.Выгрузить().ВыгрузитьКолонку("ИмяКолонки");
	
	СтрокаКолонкиГруппировок = СтрСоединить(МассивГруппировки, ",");
	СтрокаКолонкиСумм = СтрСоединить(МассивСумм, ",");
	
	ДобавитьКолонкиТаблицыСравнение(МассивГруппировки, МассивСумм);

	
    ТаблицаЗначений1 = ПолучитьДанныеТаблицы("ТабличныйДокумент1");
	ТаблицаЗначений2 = ПолучитьДанныеТаблицы("ТабличныйДокумент2");
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	Для Каждого СтрокаТЗ Из ТаблицаПолейГруппировок Цикл
		ТаблицаЗначений.Колонки.Добавить(СтрокаТЗ.ИмяГруппировки);		
	КонецЦикла;	
	Для Каждого СтрокаТЗ Из ТаблицаПолейСумм Цикл
		ТаблицаЗначений.Колонки.Добавить(СтрокаТЗ.ИмяКолонки, Новый ОписаниеТипов("Число"));		
	КонецЦикла;

	Для Каждого СтрокаТЗ Из ТаблицаЗначений1 Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		Для Каждого Колонка Из МассивГруппировки Цикл
			НоваяСтрока[Колонка] = ВРег(СтрокаТЗ[Колонка]); 
		КонецЦикла;	
	КонецЦикла;
	Для Каждого СтрокаТЗ Из ТаблицаЗначений2 Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		Для Каждого Колонка Из МассивСумм Цикл
			НоваяСтрока[Колонка] = НоваяСтрока[Колонка] * (-1);
		КонецЦикла;
		Для Каждого Колонка Из МассивГруппировки Цикл
			НоваяСтрока[Колонка] = ВРег(СтрокаТЗ[Колонка]); 
		КонецЦикла;
	КонецЦикла;
	ТаблицаЗначений.Свернуть(СтрокаКолонкиГруппировок, СтрокаКолонкиСумм);
	МассивУдаления = Новый Массив;
	Для Каждого СтрокаТЗ Из ТаблицаЗначений Цикл
		флагПустая = Истина;
		Для Каждого Колонка Из МассивСумм Цикл
			Если СтрокаТЗ[Колонка] <> 0 Тогда
				флагПустая = Ложь;
			КонецЕсли;	
		КонецЦикла;
		Если флагПустая Тогда
			МассивУдаления.Добавить(СтрокаТЗ);
		КонецЕсли;		
	КонецЦикла;
	Для Каждого СтрокаТЗ Из МассивУдаления Цикл
		ТаблицаЗначений.Удалить(СтрокаТЗ);
	КонецЦикла;	
	
	ТаблицаСравнение.Загрузить(ТаблицаЗначений);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкиТаблицыСравнение(Знач МассивГруппировки, Знач МассивСумм)
	
	МассивУдаляемыхРеквизитов = Новый Массив;
	МассивУдаляемыхРеквизитов1 = ЭтаФорма.ПолучитьРеквизиты("ТаблицаСравнение");
	Для Каждого ТекущийРеквизит Из МассивУдаляемыхРеквизитов1 Цикл
		МассивУдаляемыхРеквизитов.Добавить(СтрШаблон("%1.%2", ТекущийРеквизит.Путь, ТекущийРеквизит.Имя));
	КонецЦикла;	
	ИзменитьРеквизиты(, МассивУдаляемыхРеквизитов);
	МассивУдаляемыхЭлементов = Новый Массив;
	Для Каждого ТекущийЭлемент Из Элементы.ТаблицаСравнение.ПодчиненныеЭлементы Цикл
		МассивУдаляемыхЭлементов.Добавить(ТекущийЭлемент);		
	КонецЦикла;
	Для Каждого ТекущийЭлемент Из МассивУдаляемыхЭлементов Цикл
		Элементы.Удалить(ТекущийЭлемент);
	КонецЦикла;	
	
	МассивДобавляемыхРеквизитов = Новый Массив; 
	Для Каждого ИмяКолонки Из МассивГруппировки Цикл											 
		МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы(ИмяКолонки, Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(200)),"ТаблицаСравнение" , ИмяКолонки)); 		
	КонецЦикла;	
	Для Каждого ИмяКолонки Из МассивСумм Цикл											 
		МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы(ИмяКолонки, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(20, 6)),"ТаблицаСравнение" , ИмяКолонки)); 		
	КонецЦикла;
	ИзменитьРеквизиты(МассивДобавляемыхРеквизитов); 	
	Для Каждого Колонка Из МассивДобавляемыхРеквизитов Цикл
		ИмяКолонки = Колонка.Имя;
		
		НовыйЭлемент  = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), Элементы.ТаблицаСравнение);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
		НовыйЭлемент.ПутьКДанным = СтрШаблон("ТаблицаСравнение.%1", ИмяКолонки);		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СравнитьТаблицы(Команда)
	СравнитьТаблицыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицыНаСервере()
	ТабличныйДокумент1.Очистить();
	ТабличныйДокумент2.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицы(Команда)
	ОчиститьТаблицыНаСервере();
КонецПроцедуры


