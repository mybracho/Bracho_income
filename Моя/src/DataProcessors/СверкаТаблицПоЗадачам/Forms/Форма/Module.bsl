
&НаСервере
Процедура СвернутьТаблицуНаСервере()
    ТаблицаЗначений = ПолучитьТаблицаДанных();    
    ТаблицаЗначений.Свернуть("Задача","КоличествоЧасов");
    Результат.Очистить();
    
    Макет = ПолучитьМакетНаСервере();
    ОблШапка  = Макет.ПолучитьОбласть("Шапка");
    ОблСтрока = Макет.ПолучитьОбласть("Строка");
    Результат.Вывести(ОблШапка);
    Для Каждого СтрокаТз Из ТаблицаЗначений Цикл
        ЗаполнитьЗначенияСвойств(ОблСтрока.Параметры,СтрокаТЗ);
        Результат.Вывести(ОблСтрока);
    КонецЦикла; 
КонецПроцедуры

Функция ПолучитьМакетНаСервере(ИмяМакета = "Макет")
    ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
    Возврат ОбработкаОбъект.ПолучитьМакет(ИмяМакета);
КонецФункции 

&НаКлиенте
Процедура СвернутьТаблицу(Команда)
    СвернутьТаблицуНаСервере();
КонецПроцедуры

Функция ПолучитьТаблицаДанных()
    ТаблицаЗначений = Новый ТаблицаЗначений;
    ТаблицаЗначений.Колонки.Добавить("Задача");
    ТаблицаЗначений.Колонки.Добавить("КоличествоЧасов",Новый ОписаниеТипов("Число"));
    
    ВысотаТаблицы = Результат.ВысотаТаблицы;
    Для Сч = 2 По ВысотаТаблицы Цикл
        Задача = Результат.Область("R" + Сч + "C1").Текст;
        Часы =   Результат.Область("R" + Сч + "C2").Текст;
        
        НоваяСтрока = ТаблицаЗначений.Добавить();
        НоваяСтрока.Задача          = Задача;
        НоваяСтрока.КоличествоЧасов = Часы;
    КонецЦикла;
    Возврат ТаблицаЗначений;
КонецФункции 

&НаСервере
Процедура СформироватьТаблицуДляАктаНаСервере()
    Цена = Объект.Цена;
    
    Макет     = ПолучитьМакетНаСервере("Акт");
    ОблШапка  = Макет.ПолучитьОбласть("Шапка");
    ОблСтрока = Макет.ПолучитьОбласть("Строка");
    ОблИтог   = Макет.ПолучитьОбласть("Итог");
    
    тзДанных = ПолучитьТаблицаДанных();
    Результат.Очистить();
    
    ИтогСумма     = 0;
    ИтогНДС       = 0;
    ИтогСуммаСНДС = 0;
    
    Результат.Вывести(ОблШапка);
    Счетчик = 0;
    Для Каждого СтрокаТЗ Из тзДанных Цикл
        Сумма =  Цена*СтрокаТЗ.КоличествоЧасов;
        
        Счетчик = Счетчик+1;
        ОблСтрока.Параметры.НомерПП      = Счетчик;
        ОблСтрока.Параметры.Наименование = СтрокаТЗ.Задача;
        ОблСтрока.Параметры.ЕдИзм        = "час";
        ОблСтрока.Параметры.Количество	 = СтрокаТЗ.КоличествоЧасов;
        ОблСтрока.Параметры.Цена	     = Цена;
        ОблСтрока.Параметры.Сумма        = Сумма;
        
        ИтогСумма     = ИтогСумма+Сумма;
        ИтогСуммаСНДС = ИтогСуммаСНДС+Сумма;
        
        Результат.Вывести(ОблСтрока);
    КонецЦикла; 
    
    ОблИтог.Параметры.ИтогСумма     = ИтогСумма;
    ОблИтог.Параметры.ИтогНДС       = ИтогНДС;
    ОблИтог.Параметры.ИтогСуммаСНДС = ИтогСуммаСНДС;
    Результат.Вывести(ОблИтог);
    
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТаблицуДляАкта(Команда)
    СформироватьТаблицуДляАктаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицуНаСервере()
    Результат.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицу(Команда)
    ОчиститьТаблицуНаСервере();
    ЗаполнитьДанныеШапки();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    Объект.Цена = Константы.ЦенаЧасаРаботы.Получить();
    ЗаполнитьДанныеШапки();
    
КонецПроцедуры

Процедура ЗаполнитьДанныеШапки()
    ТД = Результат;
    ТД.Очистить();
    
    Макет    = ПолучитьМакетНаСервере("Макет");
    ОблШапка = Макет.ПолучитьОбласть("Шапка");
    ТД.Вывести(ОблШапка); 
КонецПроцедуры 
